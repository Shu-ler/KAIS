#Область ПрограммныйИнтерфейс

// Обновить список заказов.
// 
// Параметры:
//  Профиль - СправочникСсылка.МФ_ПрофилиKAIS - Профиль
// 
// Возвращаемое значение:
//  Структура - Обновить список заказов:
// * Ошибка - Строка - 
// * НеизвестныеПоставщики - Массив Из Произвольный - Поставщики 
Функция ОбновитьСписокЗаказов(Профиль) Экспорт

	РезультатОбновления = Новый Структура;

	HTTPСоединение = Неопределено;
	ОбновленоЗаписей = 0;
	ИмяМетода = "GET";
	Продолжать = Истина;
	Сообщение = Новый СообщениеПользователю;

	НеизвестныеПоставщики = Новый Массив;

	Заголовки = Заголовки(Профиль);
	
	// Получение набора профилей
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
				   |	КАИС_Профили.Ссылка
				   |ИЗ
				   |	Справочник.КАИС_Профили КАК КАИС_Профили
				   |ГДЕ
				   |	КАИС_Профили.ПометкаУдаления = ЛОЖЬ";
	НаборПрофилей = Запрос.Выполнить().Выбрать();
	
	// Обработка профилей
	Пока НаборПрофилей.Следующий() Цикл

		Профиль = НаборПрофилей.Ссылка;

		АдресРесурсаШаблон = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Профиль, "РесурсЗаказы");
		БезДетализации = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Профиль, "РежимЗаказовБезДетализации");

		Продолжать = УстановитьСоединениеССайтом(HTTPСоединение, Заголовки, Профиль);

		Если Не Продолжать Тогда
			Продолжить;
		КонецЕсли;

		Страница = 0;
		ПродолжатьЦикл = Истина;

		Пока ПродолжатьЦикл Цикл

			АдресРесурса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(АдресРесурсаШаблон,
																				   Формат(Страница, "ЧГ=0;"),
																				   БезДетализации);
			АдресРесурса = АдресРесурса + "&filters=%7B%22date_period%22%3A%22INTERVAL+1+MONTH%22%7D";
			Страница = Страница + 1;
			
			// Создать HTTP-запроса.
			HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса, Заголовки);
			
			// Получить ответ сервера в виде объекта HTTPОтвет.
			Результат = HTTPСоединение.ВызватьHTTPМетод(ИмяМетода, HTTPЗапрос);

			Если Результат.КодСостояния <> 200 Тогда

				Сообщение.Текст = Строка(Результат.КодСостояния);
				Сообщение.Сообщить();

				Сообщение.Текст = ОписаниеОшибки();
				Сообщение.Сообщить();

				РезультатОбновления.Вставить("Ошибка", ОписаниеОшибки());
				РезультатОбновления.Вставить("НеизвестныеПоставщики", НеизвестныеПоставщики);

				Возврат РезультатОбновления;

			КонецЕсли;

			Ответ = Результат.ПолучитьТелоКакСтроку(КодировкаТекста.UTF8);

			ПродолжатьЦикл = ОбработатьСтраницуЗаказов(Профиль, Ответ, ОбновленоЗаписей, НеизвестныеПоставщики);

		КонецЦикла;

		Константы.КАИС_Обновлено.Установить(ТекущаяДатаСеанса());

		Сообщение.Текст = "Обновлено записей о заказах клиентов - " + Строка(ОбновленоЗаписей);
		Сообщение.Сообщить();

	КонецЦикла;

	РезультатОбновления.Вставить("НеизвестныеПоставщики", НеизвестныеПоставщики);

	Возврат РезультатОбновления;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Формирует заголовки для HTTP - соединения
// 
// Параметры:
//  Профиль - СправочникСсылка.МФ_ПрофилиKAIS - Профиль
// 
// Возвращаемое значение:
//  Соответствие из Строка - Заголовки
Функция Заголовки(Профиль)

	Хост = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Профиль, "Адрес");
	Реферер = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку("http://%1/", Хост);

	Заголовки = Новый Соответствие;

	Заголовки.Вставить("Host", Хост);
	Заголовки.Вставить("Connection", "keep-alive");
	Заголовки.Вставить("Upgrade-Insecure-Requests", "1");
	Заголовки.Вставить("User-Agent",
					   "Mozilla/5.0 (Windows NT 10.0; Win64; x64) AppleWebKit/537.36 (KHTML, like Gecko) Chrome/96.0.4664.110 Safari/537.36 Edg/96.0.1054.62");
	Заголовки.Вставить("Accept",
					   "text/html,application/xhtml+xml,application/xml;q=0.9,image/webp,image/apng,*/*;q=0.8,application/signed-exchange;v=b3;q=0.9");
	Заголовки.Вставить("Referer", Реферер);
	//	Заголовки.Вставить("Accept-Encoding", "gzip, deflate");
	Заголовки.Вставить("Accept-Language", "ru,en;q=0.9,en-GB;q=0.8,en-US;q=0.7");
	Заголовки.Вставить("Cookie",
					   "PHPSESSID=pe8m04cl41iotc59f795itv9hfgfjue8; auth=69dad64f39ab8925696e61891f197f29; _ym_isad=2; company_id=79; notice_seen=1640671260; shop_brand_size=24px");
	Заголовки.Вставить("Accept-Encoding", "identity");

	Возврат Заголовки

КонецФункции

#КонецОбласти